@set currentLocation="Hospital"
@hideChars
@hidePrinter 
@hideUI MapUI
@resetText 
@back hospital

@char Lucy.Smile01 look:right pos:15,0 scale:0.65,0.65
@animate Lucy posX:25 time:1 wait:true

@if isFourthQestCollected=true

@char Lia.Normal01 look:right pos:85 scale:0.5,0.5
@animate Lia posX:75

Lucy.Smile01: You're back. Did you find it?

Me: Yes. I found it.

Lia.Normal02: So what's next? Are you going to keep the item or are you going to give it to one of us?

Me: Let me think...

@choice "Give the item to Lucy" handler:MyButtonList goto:.GiveToLucy
@choice "Give the item to Lia" handler:MyButtonList goto:.GiveToLia
@choice "Keep it for yourself" handler:MyButtonList goto:.KeepForSelf

@hidePrinter

@stop

# GiveToLucy
@sfx ButtonClick
@set itemOwner="Lucy"
Lucy.Smile04: Hah! I knew you'd make the right call.
@char Lia.Argue02
@goto .FinalReaction

# GiveToLia
@sfx ButtonClick
@set itemOwner="Lia"
Lucy.Angry01: You're giving it to her? After everything?
@char Lia.Normal02
@goto .FinalReaction

# KeepForSelf
@sfx ButtonClick
@set itemOwner="Me"
Lucy.Angry02: Keeping it for yourself? Bold move...
@char Lia.Angry01
@goto .FinalReaction

@endif

@if isFirstQuestCollected=false

Lucy.Smile02: <b>{name}</b>, you finally woke up!

Me: Who are you?

@char Lucy.Neutral01 look:left
Lucy.Sad01: It's me, Lucy. We've met before... though you might not remember.
Me: I don't. Everything feels... empty.

@char Lucy.Neutral01 look:right
Lucy.Neutral01: That makes sense. You were found unconscious near the edge of the old sector.

@char Lucy.Neutral01 look:left
Lucy.Smile01: If you want to find out who you really are — go to the <b>Office</b>. Look for a woman named <b>Lia</b>. She might help you remember.

@resetQuest name:"New Acquaintances"
@addQuest name:"New Acquaintances" info:"Find Lia in the Office to learn more about your past."

@gosub .FirstQuestTip

@set isOfficeUnlocked=true
@set isFirstQuestCollected=true

@elseif isCardGameCompleted=true

Lucy.Smile02: Then don’t waste time. The room might still be untouched.

@else

Lucy.Smile01: If you want to find out who you really are — go to the office buiding. Look for a woman named Lia.
@gosub .FirstQuestTip

@endif

@showUI MapUI

@stop

# FirstQuestTip
@char Lucy.Neutral01 look:left
Lucy.Neutral01: You can visit the <b>Office</b> using the map.
Lucy.Smile01: Just click the <b>Map</b> button — it’s in the top-right corner of the screen.
@return

# FinalReaction

@completeQuest name:"Final Decision"

@if itemOwner="Lucy"
Lia.Angry01: So... that's it? You chose her?

@elseif itemOwner="Lia"
Lucy.Angry02: She's playing you. You'll regret it.

@elseif itemOwner="Me"
@char Lucy.Angry01 look:right
Lucy.Angry02: So you don’t trust anyone. Typical.
@char Lia.Angry01 look:left
Lia.Angry01: You think keeping it will help? Fine. Do it your way.

@endif

@stop
